module sdf2imp/project/create-parse-table

imports
  libstratego-lib
  libstratego-xtc
  
  EditorService
  
  sdf2imp/project/-
  sdf2imp/services/-
  sdf2imp/util/-
  sdf2imp/lib/-

strategies
  
  copy-or-create-parse-table =
    copy-parse-table <+ xtc-ensure-file; create-parse-table <+ fatal-err(|"Could not copy or create parse table")
  
  copy-parse-table =
    <eq> (<ParseTable; abspath>, <parse-table-target; abspath>) // skip copying
  <+
    ParseTable => "None" // skip copying
  <+
    ParseTable;
    if file-exists then
      print-filename(|[], <parse-table-target>);
      <copy-file> (<id>, <parse-table-target>)
    end
    
  create-parse-table:
    FILE(input) -> FILE(output)
    with
      FILE(input') := <create-permissive-grammar> FILE(input);
      output       := <parse-table-target>;
      print-filename(|[], output);
      <debug; sdf2table> (input', output, [])
  
  // (Note that these xtc calls are intercepted by the Spoofax/IMP runtime.)
  sdf2table =
    ?(input, output, options);
    <xtc-command(!"sdf2table")> ["-i", <abspath> input, "-o", <abspath> output, "-m", <get-sdf-main-module> | <conc> (options, <pass-v-verbose>)]
  <+
    if <xtc-command(!"sdf2table")> ["-V"] then // ensure it's available
      fatal-err(|["Internal error: parse table creation failed"])
    else
      fatal-err(|["Internal error: could not call sdf2table"])
    end
  
  create-permissive-grammar:
    FILE(input) -> FILE(output)
    with
      output := <permissive-grammar-target>;
      print-filename(|[], output);
      args   := ["-i", input, "-o", output, "--open-brackets", "on", "--optimize", "on"];
      if-java-platform(
        <call> ("org.strategoxt.permissivegrammars.main-make-permissive", args)
      );
      if-c-platform(  // FIXME: should we even bother making make-permissive an xtc executable?
        <call> ("make-permissive", args)
      )
    <+
      warn(|"Could not create permissive grammar using make-permissive");
      output := input

  permissive-grammar-target =
    <file-exists <+ mkdir> "include";
    <conc-strings> ("include/", <get-sdf-main-module>, "-Permissive.def")

  parse-table-target =
    <file-exists <+ mkdir> "include";
    <conc-strings> ("include/", <get-sdf-main-module>, ".tbl")
