module context/sdf/declare
imports
	libstratego-sdf
  lib/editor-common.generated
	include/SpoofaxLang
imports
	context/-
	context/esv/declare
	codegen/-
	analyze/main
	normalize/signatures
	context/sdf/declare-signature
imports
	common
	utils/debug
	utils/annotations
	utils/attributes
	utils/contract
imports
	libstratego-sdf
  lib/editor-common.generated
	include/SpoofaxLang
	normalize/main
  
rules
	setof-sdf-declarations = resolve-sdf-declarations
	
	bagof-sdf-declarations = <resolve-all-symbol-definitions-with-duplicates>(<id>, SDFDeclaration())
			  		
  resolve-sdf-declarations =
    <resolve-all-symbol-definitions>(<id>, SDFDeclaration())
  <+
    dbg-analysis(! $[ErrorChecking| FAIL| Failed to retrive SDF declaration for symbol [<print-as-string>]] | "resolve-sdf-declarations" ,<debug-level-WARNING>); 
    fail

  resolve-atleast-one-sdf-declaration =
    <resolve-symbol-definition>( <id>, SDFDeclaration())
  <+
    dbg-analysis(! $[ErrorChecking| FAIL| Failed to retrive SDF declaration for symbol [<print-as-string>]] 
                 | "resolve-atleast-one-sdf-declaration" ,<debug-level-WARNING>)
    ; fail

  resolve-NamespaceSort-declaration =
    <resolve-symbol-definition>( <to-scoped-symbol>, NamespaceSortDeclaration())
  <+
    dbg-analysis(! $[ErrorChecking| FAIL| Failed to retrive NamespaceSort declaration for symbol [<print-as-string>]] 
                 | "resolve-NamespaceSort-declaration" , <debug-level-WARNING>)
    ; fail

     

rules
 // Traverse AST and declare sdf definition
 //	Instead of declaring in the Global Scope
 // now it is declared in a particular Scope.
 // Here scope is defined by (Package-QName, Module-QName)
 declare-sdf-definition-main = alltd(declare-sdf-definition)
 
 declare-sdf-usage-main = alltd(declare-sdf-usage)

	declare-legacy-sdf-imports = 	
		   activate-global-scope(
		   		alltd(declare-sdf-definition-main)			
		   )
		   
rules
	declare-sdf-definition :
		context-free-start-symbols(s*) -> <id>
		where <declare-sdf-definition-main>s*
		where store-context-free-start-symbols(|<GetEnclosingModuleName>)

rules
	
	declare-sdf-definition:
    renamings(y) -> <id>
    where	<all(try(?symbol(_, <id>); declare-sdf-simple-definition))> y


	declare-sdf-definition:
    parameterized(x, y) -> parameterized(x, y)
    where	<declare-sdf-definition> unparameterized(x);
    			<alltd(declare-sdf-simple-definition)> y

rules

  declare-sdf-definition: p@prod(lhs, sort, attrs) -> prod(lhs, sort, attrs)  where	<declare-sdf-definition-internal> (p, lhs, sort, attrs) 
  
 	declare-sdf-definition-internal:
    (prod, lhs, sort, attrs) -> <id>
    where dbg-declaration(! $[declaring sdf : [<print-as-string>prod]]| "declare.declare-sdf-definition-internal" , <debug-level-INFO>)
    where	x  := <get-rhs-sort-name>sort
    where if <is-lexical-sort>sort + IsInsideLexicalSyntaxBlock then 
    				sortType :=  LEXSort()
    			else
    				sortType :=  CFSort()
    			end 		
    where	<declare-signature>prod
  	// TODO FIX : enable namespace annotation. currently it is not fully supported 
  	//where	with-spxverify(<topdown(try(declare-namespace-def))> lhs	,	!"declare-sdf-definition-internal | Failed while storing namespace-def ")
  	where	with-spxverify(
      			if <fetch-cons-name> attrs => cons then
	      			tuple := (x, cons);
	       			<index-symbol-definition>(tuple , SDFDeclaration() , prod ) // Needed for Sort.Cons references to the production.
	      		end
	    			, !"Failed while storing sort.cons references at declare.declare-sdf-definition-internal"      
     		 	)
	 	where with-spxverify(GetEnclosingModuleName ,"Failed, since current module scope is not declared at declare.declare-sdf-definition-internal." )
	  where	with-spxverify(
      			 
      			<index-symbol-definition>(x, SDFDeclaration(),prod );
      			<index-symbol-definition>(x , SortDeclaration(), (x, sortType));
    	  		dbg-declaration(
    	  			!$[Storing Declaration : [<print-as-string>x]  -> [<print-as-string>prod]]  
    	  			| "declare.declare-sdf-definition-internal" 
    	  			, <debug-level-INFO> 
	  			  ) 
      			, !"Failed to index production at declare.declare-sdf-definition-internal"
      		) 
 
  declare-signature:
  	p@prod(lhs, sort, attrs) -> <id>
  	where with-spxverify(
						if not(<attrs(fetch(?reject()))> attrs) then
	        		<store-operation-declaration>p
	      		end
      			, !$[Failed while trying to store operation declaration with following production [<print-as-string> p] at declare.declare-sdf-definition-internal]
  				)
 
 declare-namespace-def:
    namespacedef(name, sort) -> <id>
    where with-spxverify(	scoped-name  := <to-scoped-symbol> name	)
    where <index-symbol-definition>(scoped-name, NamespaceSortDeclaration(),sort)

  declare-sdf-definition:
		lex-syntax@lexical-syntax(lex-defintions) ->lex-syntax
		where 
			with-spxverify(
				{| IsInsideLexicalSyntaxBlock:
								mark-as-lexical-syntax-block;
								<declare-sdf-definition-main> lex-defintions
				|}
			)
			
	declare-sdf-definition:
		parameterized(x, y) -> parameterized(x, y)
  	where	<declare-sdf-definition> unparameterized(x);
  				<alltd(declare-sdf-simple-definition)> y


	declare-sdf-simple-definition:
		sort(x) -> <id>
		where with-spxverify(
	  				scoped-x:= <to-scoped-symbol>x;
	  				<index-symbol-definition>(scoped-x , SDFDeclaration(),x )
  				)
		where	dbg-declaration(!$[Storing Declaration [<print-as-string>scoped-x] -> [<print-as-string>x]] | "SDF" , <debug-level-INFO> )


rules
	declare-sdf-usage: 'sorts(s*) -> 'sorts(s') where <alltd(declare-sdf-usage)>s* => s' 
			
	declare-sdf-usage: context-free-start-symbols(s*) -> context-free-start-symbols(s') where <alltd(declare-sdf-usage)>s* =>s'
		
  declare-sdf-usage: lexical-start-symbols(s*) -> lexical-start-symbols(s') where <alltd(declare-sdf-usage)>s* =>s'
  	
	declare-sdf-usage: kernel-start-symbols(s*) -> kernel-start-symbols(s') where <alltd(declare-sdf-usage)>s* =>s'
    
  declare-sdf-usage: prod(lhs, sort, attrs) -> prod( lhs' , sort, attrs) where <alltd(declare-sdf-usage)>lhs => lhs'
  
  declare-sdf-usage: prod(lhs, sort, attrs) -> prod( lhs' , sort, attrs) where <alltd(declare-sdf-usage)>lhs => lhs'
  
  declare-sdf-usage:
    QualifiedSort(Name(_), x) -> result 
  	where basic-sdf-desugar
  				;	declare-sdf-usage
  				; ?result   
  
  declare-sdf-usage:
    QualifiedSort(q@QName(_), x) -> QualifiedSort(q' , x)
    where	!q;
    			((is-declared-package;!Package(q))  
			<+  (is-declared-module; !Module(q) ; get-enclosing-package-qname) 
			<+  !Unresolved())=> q'
	  where if not(!q' => Unresolved()) then 
	  				with-spxverify( <index-sdf-usage(|q')>x , !"Indexing Qualified SDF Sort Call Sites failed. ")
	  			end	
	  			
	declare-sdf-usage:
    sort(x) -> <id>
    where with-spxverify( <index-sdf-usage(|Unqualified())>x , !"Indexing SDF Sort Call Sites failed. ") 

	// For legacy artifacts only.
	// It is required for import def or sdf file. currently we support parameterized sort in 
	// a different way in spoofaxlang.
	declare-sdf-usage:
    parameterized-sort(x, y) -> <id>
    where
      if !y => [sort(y')]; <is-string> x; <is-string> y' then
        text := ${{x}[[{y'}]]};
       	<index-sdf-usage(|Unqualified())>( text , text)
      end

rules
	
	index-sdf-usage(|namespace):
		sortName-> <id>
		where is-string
		where if <not(verify-call-sites-already-registered(|namespace))>sortName then
			  			<resolve-all-symbol-definitions(
								fetch-sort-and-context
								|(LookupAllScope(), <ALL_AS_SET>)
							)>(sortName,  SortDeclaration())
							; is-not-empty-list 
							;	(		(filter(?(_, _, InternalScope())); is-not-empty-list)   
								<+	(filter(?(_, _, GlobalScope()))  ; is-not-empty-list)    
								<+  (filter(?(_, _, PublicScope()))  ; is-not-empty-list) 
								)
							;	?[(s,c,t)|_] 
							; !(s,(s,c,t))
							; index-sdf-usage(|namespace)
					<+ 
							dbg-declaration(
								!$[ Failed to find sort : [<print-as-string>sortName]] 
               	| "declare.index-sdf-usage" , <debug-level-WARNING>
               	)
    		 end
					   
	index-sdf-usage(|namespace) : 
  	(sortName , symbolDef) -> <id>
  	where with-spxverify( 
  					<index-unique-symbol-definition> ((sortName,namespace), SDFUsage() ,symbolDef) 
					, "Indexing  SDF Call Sites Failed"
					)
					 
  get-sdf-usage(|namespace) :
  	sortName -> r 
  	where
  		activate-scope(
  			<resolve-all-symbol-definitions(project-from-symboldef-to-symbol |(LookupLocalScope(), <ONLY_ONE>))> ((sortName , namespace),  SDFUsage())
				; ensure-is-unique
				; ?r
			|	<GetEnclosingPackageName>
			)
  
  fetch-sort-and-context: 
		SymbolDef(m@Module(QName(_)), t1 ,t2, s, t4) -> <fetch-sort-and-context> SymbolDef(<get-enclosing-package-qname>m, t1 ,t2, s, t4)
		where <is-string>s 
	
	fetch-sort-and-context: 
		SymbolDef(p@Package(QName(_)), _ ,_, sortName, _) -> (sortName , p, resolved-from)
		where <is-string>sortName 
		where GetEnclosingPackageName => enclosing-package
		where if <?p>enclosing-package then 
						resolved-from := InternalScope()
					else
						resolved-from := PublicScope()
					end

	fetch-sort-and-context: 
		SymbolDef(Globals(), _ ,_, sortName, _) -> (sortName , Globals(), GlobalScope())
 		where <is-string>sortName
	
	fetch-sort-and-context: SymbolDef(context, k ,t, (sortName,_), is-overloaded) -> <fetch-sort-and-context>SymbolDef(context, k ,t, sortName , is-overloaded)

	verify-call-sites-already-registered(|namespace): 
		sortName  ->  <id>
		where is-string 
		where activate-scope(
						<get-sdf-usage(|namespace)>sortName => result 
						|<GetEnclosingPackageName>
					)
	
	verify-call-sites-already-registered(|namespace)= 
  	?sort(<id>);
  	verify-call-sites-already-registered(|namespace)
  	
signature constructors 
	SDFSortType : Type 
	CFSort   : SDFSortType 
	LEXSort  : SDFSortType