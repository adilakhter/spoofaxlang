module templatelang-to-esv-4

language SpoofaxLang
target language EditorService

setup [[
  package example
  module example
]]

target setup [[
  module example
  completions
]]

test completion templates for lexical syntax [[
  lexical syntax
    "||" -> BoolMethodId
]]
run test-templatelang-to-esv to [[
  completion template BoolMethodId: "||" = "||"
]]

test completion templates for lexical syntax (sugared) [[
  lexical syntax
    BoolMethodId = "||"
]]
run test-templatelang-to-esv to [[
  completion template BoolMethodId: "||" = "||"
]]

test completion templates from lexical syntax expanded [[
  lexical syntax
    BoolMethodId = "||"
  templates
    Exp.BinOp = <<Exp> <BoolMethodId> <Exp>>
]]
run test-templatelang-to-esv to [[
  completion template BoolMethodId: "||" = "||"
  completion template Exp: "Exp || Exp" = <Exp:Exp> " " <||:BoolMethodId> " " <Exp:Exp>
]]

test no completion template for reject production [[
  lexical syntax
    "if" -> ID {reject}
]]
run test-templatelang-to-esv to [[
]]

test no expansion for reject production [[
  lexical syntax
    "if" -> ID {reject}
  templates
    Exp.Var = <*<ID>>
]]
run test-templatelang-to-esv to [[
	completion template Exp: "*ID" = "*" <ID:ID>
]]
