module sdf2imp/project/create-packed-descriptor-file

imports
  libstratego-lib
  sdf2imp/services/common
  sdf2imp/util/input
  sdf2imp/util/output

strategies

  create-packed-descriptor-file =
    {| InputDirPrefix, OnImportFailure:
      rules(
        OnImportFailure:
          Import(file) -> <fatal-err-msg(|["Could not import ", file])>
      );
      <add-input-dir-prefix> "editor/";
      filename := <packed-descriptor-file-name>;
      main     := <main-descriptor-name>;
      
      <input-descriptor-file> main;
      remove-empty-sections;        
      output-packed-descriptor-file(|filename)
    <+
      fatal-err-msg(|"Could not pack descriptor files")
    |}
  
  remove-empty-sections =
    Module(
      id
    , id
    , filter(not(?_#([_, []])))
    )

  packed-descriptor-file-name =
    <conc-strings> (<get-sdf-main-module>, ".packed.esv")
