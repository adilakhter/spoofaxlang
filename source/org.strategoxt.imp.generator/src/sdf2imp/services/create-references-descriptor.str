module sdf2imp/services/create-references-descriptor

imports
  libstratego-lib
  libstratego-sdf
  
  sdf2imp/util/-
  sdf2imp/main

strategies

  create-references-descriptor =
    output-initial-descriptor-file(
      <descriptor-name> "-References"
    , !|[
        module <descriptor-name> "-References"
        
        imports <descriptor-name> "-References.generated"
        
        references
          ~~
          ~~// This file can be used to specify reference resolving and hover help, and content completion.
          ~~//
          ~~// See the imported file for a brief introduction and examples.
          ~~
        references
          ~~
          reference _ : editor-resolve
          hover     _ : editor-hover
      ]|
    );
    create-derived-references-descriptor
    
  create-derived-references-descriptor =
    not(is-heuristic-services-needed);
    verbose-msg(!"Skipping", <descriptor-name> "-References.generated")
  <+
    output-derived-descriptor-file(
      <descriptor-name> "-References.generated"
    ,
      !|[
        module <descriptor-name> "-References.generated"
        
        references
          ~
          ~// Reference resolving rules can specify the syntax constructors for which
          ~// a reference resolving function is available:
          ~//
          ~//   reference FunctionCall : resolve-function-call 
          ~//
          ~// This defines that for each FunctionCall, resolve-function-call
          ~// will retrieve the AST node that declares it.
          ~//
          ~// Likewise, hover help can be specified as follows:
          ~//
          ~//   hover FunctionCall : hover-help-function-call
          ~//
          ~// Reference resolving uses the semantic provider defined in YourLanguage-Builders.esv
          ~// All semantic services may make use of the dynamic rules created by the observer.
      ]|
    )
